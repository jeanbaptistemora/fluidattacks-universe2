:slug: kb/cobol/usar-estructuras-condicionales/
:eth: no
:category: cobol
:kb: yes

= Usar Estructuras Condicionales

== Necesidad

Usar estructuras condicionales contemplando 
todos los escenarios posibles 
y valores por defecto en COBOL.

== Contexto

A continuación se describe las circunstancias
bajo las cuales la siguiente solución tiene sentido:

. Se dispone de una aplicación desarrollada en COBOL.
. El código debe ser invocado en algún escenario [5].
. El código debe de inicializar las variables que crea [6].

== Solución

En cada estructura condicional empleada en una aplicación COBOL, 
se recomienda contemplar todos los posibles escenarios, 
como se muestra a continuación, 
iniciamos la aplicación de prueba 
declarando la división de identificación:
+
[source,cobol,linenums]
----
       IDENTIFICATION DIVISION.
      ******************
      * Identification *
      ******************
       PROGRAM-ID. COBOLCOND.
----
La división de datos _DATA DIVISION_ contiene dos variables: 
PLANET-NUMBER de tipo numérico con dos dígitos 
y PLANET-NAME con soporte para ocho caracteres. 
En cumplimiento del requisito: 
_"El código debe de inicializar las variables que crea."_, 
se deben inicializar todos los valores de las variables 
a través del uso de la palabra clave _VALUE_, 
para este caso, "3" para el número del planeta 
y "Tierra" para el nombre:
+
[source,cobol,linenums]
----
      ********
      * Data *
      ********
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01 PLANET.
           05 PLANET-NUMBER PIC 9(2) VALUE 3.
           05 PLANET-NAME   PIC X(8) VALUE "Tierra".
----
En la división de procedimiento _PROCEDURE DIVISION_
se encuentra la lógica del programa:
+
[source,cobol,linenums]
----
      ********
      * Main *
      ********
       PROCEDURE DIVISION.
       MAIN.
----
En un ciclo escogemos los planetas impares 
(El valor inicial del número 
es 3 por lo que al ir sumando 2 vemos los impares), 
mostramos su número y su nombre. 
En la primer visualización, 
el programa debe imprimir 
los valores por defecto 
inicializados en la división de datos:
+
[source,cobol,linenums]
----
           DISPLAY PLANET-NUMBER, " - ", PLANET-NAME.
           
           PERFORM 3 TIMES
               COMPUTE PLANET-NUMBER = PLANET-NUMBER + 2
----
Con la palabra clave _EVALUATE_ [1] [3] 
podemos determinar el nombre del planeta según su número, 
note como se toma en cuenta el escenario por defecto 
a través de la palabra clave OTHER 
(El escenario por defecto 
es cuando el número no coincide con el de algún planeta):
+
[source,cobol,linenums]
----
               EVALUATE PLANET-NUMBER
                   WHEN 1 MOVE "Mercurio" TO PLANET-NAME
                   WHEN 2 MOVE "Venus   " TO PLANET-NAME
                   WHEN 3 MOVE "Tierra  " TO PLANET-NAME
                   WHEN 4 MOVE "Marte   " TO PLANET-NAME
                   WHEN 5 MOVE "Jupiter " TO PLANET-NAME
                   WHEN 6 MOVE "Saturno " TO PLANET-NAME
                   WHEN 7 MOVE "Urano   " TO PLANET-NAME
                   WHEN 8 MOVE "Neptuno " TO PLANET-NAME
                   WHEN OTHER MOVE "Invalido" TO PLANET-NAME
               END-EVALUATE

               DISPLAY PLANET-NUMBER, " - ", PLANET-NAME
           END-PERFORM.
----
Usamos la instrucción _ACCEPT_ 
para obtener el número ingresado por el usuario:
+
[source,cobol,linenums]
----
           DISPLAY "Ingrese un numero de planeta".
           ACCEPT PLANET-NUMBER.
----
Usando el condicional _IF_ [2] [4] 
determinamos el tipo de planeta, 
observe nuevamente como se deben considerar 
todos los escenarios por defecto 
a través del uso de la sentencia ELSE:
+
[source,cobol,linenums]
----
           IF PLANET-NUMBER IS >= 1 AND PLANET-NUMBER IS <= 8
               IF PLANET-NUMBER IS < 5
                   DISPLAY "Planeta interior"
               ELSE
                   DISPLAY "Planeta exterior"
               END-IF
           ELSE
               DISPLAY "Tipo de planeta desconocido"
           END-IF
           
           STOP RUN.
----
La salida para el anterior programa es la siguiente:
+
[source,cobol,linenums]
----
03 - Tierra
05 - Jupiter
07 - Urano
09 - Invalido
Ingrese un numero de planeta
10
Tipo de planeta desconocido
----

== Referencias

. https://www.ibm.com/support/knowledgecenter/SSQ2R2_9.1.1/com.ibm.ent.dbt.zos.doc/rmdita/rcmdeva.html[EVALUATE command (COBOL)]
. https://www.ibm.com/support/knowledgecenter/en/SS6SG3_4.2.0/com.ibm.entcobol.doc_4.2/PGandLR/tasks/tpctl05.htm[Using nested IF statements]
. http://www.fluffycat.com/COBOL/Evaluate/[COBOL Evaluate]
. http://www.fluffycat.com/COBOL/If-and-End-If/[COBOL If And End If]
. REQ.0156: El código debe definir 
opciones por defecto seguras (ej: default en sentencias switch).
. REQ.0163: Las variables del código fuente 
deben estar inicializadas de forma explícita.